#!/usr/bin/perl

use strict;
use warnings;
use 5.010;

use Template;
use Pandoc;
use FindBin '$Bin';
use File::Find;
use File::Path 'make_path';
use File::Copy;

my $tt = Template->new(
  INCLUDE_PATH => ["$Bin/../tt_lib", "$Bin/.." ],
  OUTPUT_PATH  => "$Bin/../docs",
  WRAPPER      => 'page',
  FILTERS      => {
    markdown   => sub { pandoc->convert(markdown => 'html', $_[0]) }, 
  },
);

find({ wanted => \&do_this, no_chdir => 1 }, 'in');

sub do_this {
  debug("File is: $_\n");
  return unless -f;
  debug("File::Find::dir: $File::Find::dir\n");
  my $dest = $File::Find::dir =~ s|^in/?||r;
  debug("Dest: $dest\n");

  debug("docs/$dest");
  make_path "docs/$dest";

  if (/\.tt$/) {
    my $out = s|\.tt$||r;
    $out =~ s|^in/||;

    debug("tt: $_ -> $out\n");
    $tt->process($_, {}, $out)
      or die $tt->error;
  } else {
    debug("Copy: $_ -> docs/$dest\n");
    copy $_, "docs/$dest";
  }
}

sub debug {
  warn @_ if $ENV{TT_DEBUG};
}
